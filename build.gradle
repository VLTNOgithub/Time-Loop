plugins {
    id 'dev.architectury.loom' version '1.7-SNAPSHOT' apply false
    id 'architectury-plugin' version '3.4-SNAPSHOT'
    id 'com.github.johnrengelman.shadow' version '8.1.1' apply false
}

task openBuildDir {
    doLast {
        def buildDir = file("${layout.buildDirectory.get().asFile.absolutePath}/libs").absolutePath
        if (System.properties['os.name'].toString().toLowerCase().contains('windows')) {
            def command = ['explorer', buildDir]
            new ProcessBuilder(command).start()
        } else {
            println "Skipping openBuildDir task as it is not running on Windows."
        }
    }
}

architectury {
    minecraft = project.minecraft_version
}

allprojects {
    group = rootProject.maven_group
    version = rootProject.mod_version
}

subprojects {
    apply plugin: 'dev.architectury.loom'
    apply plugin: 'architectury-plugin'

    base {
        archivesName = "${archives_name}-${project.name.toUpperCase()}-${minecraft_version}"
    }

    processResources {
        var replaceProperties =
                [
                        "mod_id"                                : mod_id,
                        "mod_version"                           : mod_version,
                        "mod_name"                              : mod_name,
                        "mod_description"                       : mod_description,
                        "mod_author_1"                          : mod_author_1,
                        "mod_author_2"                          : mod_author_2,
                        "mod_homepage"                          : mod_homepage,
                        "mod_source"                            : mod_source,
                        "mod_issues"                            : mod_issues,
                        "mod_license"                           : mod_license,
                        "mod_mocap_version"                     : mod_mocap_version,

                        "maven_group"                           : maven_group,
                        "archives_name"                         : archives_name,

                        "minecraft_version"                     : minecraft_version,
                        "yarn_mappings"                         : yarn_mappings,
                        
                        "fabric_loader_version"                 : fabric_loader_version,
                        "fabric_api_version"                    : fabric_api_version,
                        "neoforge_version"                      : neoforge_version,
                        "yarn_mappings_patch_neoforge_version"  : yarn_mappings_patch_neoforge_version,
                ]

        filesMatching(["pack.mcmeta", "fabric.mod.json", "META-INF/neoforge.mods.toml", "*.mixins.json"]) {
            expand replaceProperties
        }
        inputs.properties replaceProperties
    }

    jar {
        exclude "assets/time-loop/icon.psd"
    }
    
    if (project.name != "common") {
        jar {
            finalizedBy openBuildDir
        }
    }

    dependencies {
        minecraft "net.minecraft:minecraft:$rootProject.minecraft_version"
        mappings loom.layered {
            it.mappings("net.fabricmc:yarn:$rootProject.yarn_mappings:v2")
            it.mappings("dev.architectury:yarn-mappings-patch-neoforge:$rootProject.yarn_mappings_patch_neoforge_version")
        }
    }

    java {
        // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
        // if it is present.
        // If you remove this line, sources will not be generated.
        // withSourcesJar()

        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.release = 21
    }
}
